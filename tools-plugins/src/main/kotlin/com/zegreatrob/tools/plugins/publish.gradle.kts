package com.zegreatrob.tools.plugins

import java.nio.charset.Charset
import java.util.*

plugins {
    `maven-publish`
    signing
}

repositories {
    mavenCentral()
}

group = "com.zegreatrob.tools"

// WARNING: Do NOT refactor this afterEvaluate block to extract top-level helper functions.
//
// Commit d149003a attempted to extract configurePom/configureLicense/etc as top-level functions,
// which caused initializeSonatypeStagingRepository to fail with HTTP 403 from Sonatype.
// Root cause unclear, but likely related to project reference semantics or nexus-publish-plugin interaction.
//
// Keep all POM configuration inline within this afterEvaluate block.
// See: PUBLISH_ISSUE_ANALYSIS.md and agents.d/context/GRADLE_PLUGIN_CONSTRAINTS.md
afterEvaluate {
    publishing.publications.withType<MavenPublication>().forEach {
        with(it) {
            val scmUrl = "https://github.com/robertfmurdock/ze-great-tools"

            pom.name.set(project.name)
            pom.description.set(project.name)
            pom.url.set(scmUrl)

            pom.licenses {
                license {
                    name.set("MIT License")
                    url.set("$scmUrl/blob/main/LICENSE")
                    distribution.set("repo")
                }
            }
            pom.developers {
                developer {
                    id.set("robertfmurdock")
                    name.set("Rob Murdock")
                    email.set("rob@continuousexcellence.io")
                }
            }
            pom.scm {
                url.set(scmUrl)
                connection.set("git@github.com:robertfmurdock/ze-great-tools.git")
                developerConnection.set("git@github.com:robertfmurdock/ze-great-tools.git")
            }
        }
    }
}

signing {
    val signingKey = project.findProperty("signingKey") as String?
    val signingPassword = project.findProperty("signingPassword") as String?

    if (signingKey != null) {
        val decodedKey = Base64.getDecoder().decode(signingKey).toString(Charset.defaultCharset())
        useInMemoryPgpKeys(
            decodedKey,
            signingPassword,
        )
    }
    sign(publishing.publications)
}

tasks {
    publish { finalizedBy("::closeAndReleaseSonatypeStagingRepository") }
}
